plugins {
    id 'java-library'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
    it.options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    exclusiveContent {
        forRepository {
            maven {
                name = "Sponge"
                url = uri("https://repo.spongepowered.org/repository/maven-public/")
            }
        }
        filter { includeGroupAndSubgroups("org.spongepowered") }
    }

    exclusiveContent {
        forRepositories(
                maven {
                    name = "ParchmentMC"
                    url = uri("https://maven.parchmentmc.org/")
                },
                maven {
                    name = "NeoForge"
                    url = uri("https://maven.neoforged.net/releases/")
                }
        )
        filter { includeGroup("org.parchmentmc.data") }
    }
}

tasks.jar {
    manifest {
        attributes([
                "Specification-Title"   : "AOE Emotes",
                "Specification-Vendor"  : "Team AOE",
                "Specification-Version" : archiveVersion,
                "Implementation-Title"  : project.name,
                "Implementation-Version": archiveVersion,
                "Implementation-Vendor" : "Team AOE",
                "Built-On-Minecraft"    : project.minecraft_version
        ])
    }
}